#!/usr/bin/perl

# Copyright Â© 2019 Felix Lechner
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

# The harness for Lintian's test suite.  For detailed information on
# the test suite layout and naming conventions, see t/tests/README.
# For more information about running tests, see
# doc/tutorial/Lintian/Tutorial/TestSuite.pod
#

use strict;
use warnings;
use autodie;
use v5.10;

use Getopt::Long;
use List::Util qw(all);
use Path::Tiny;

BEGIN {
    $ENV{'LINTIAN_TEST_INSTALLED'} = 'no'
      unless exists $ENV{'LINTIAN_TEST_INSTALLED'};

    my $cwd = Cwd::getcwd();

    if ($ENV{'LINTIAN_TEST_INSTALLED'} eq 'yes') {
        $ENV{'LINTIAN_ROOT'} = '/usr/share/lintian';
    } else {
        $ENV{'LINTIAN_ROOT'} = $cwd;
    }
}

use lib "$ENV{'LINTIAN_ROOT'}/lib";

use Test::Lintian::Output::EWI;
use Test::Lintian::Output::FullEWI;
use Test::Lintian::Output::LetterQualifier;
use Test::Lintian::Output::ColonSeparated;
use Test::Lintian::Output::XML;

# options
my $format;

Getopt::Long::Configure;
unless (
    Getopt::Long::GetOptions(
        'f|format=s'  => \$format,
        'help|h'      => sub {usage(); exit;},
    )
) {
    usage();
    die;
}

# check arguments and options
die "Please use -h for usage information.\n"
  if scalar @ARGV < 1 || scalar @ARGV > 2;

# get arguments
my ($inpath, $outpath) = @ARGV;

die "File $inpath does not exist.\n"
  unless -f $inpath;

my $text = path($inpath)->slurp_utf8;
my $converted = to_universal($format, $text);

if (defined $outpath) {
    path($outpath)->spew_utf8($converted);
}else {
    print $converted;
}

exit;

sub to_universal {
    my ($format, $text) = @_;

    if ($format eq 'EWI') {
        return Test::Lintian::Output::EWI::to_universal($text);
    }elsif ($format eq 'letterqualifier') {
        return Test::Lintian::Output::LetterQualifier::to_universal($text);
    }elsif ($format eq 'fullewi') {
        return Test::Lintian::Output::FullEWI::to_universal($text);
    }elsif ($format eq 'colons') {
        return Test::Lintian::Output::ColonSeparated::to_universal($text);
    }elsif ($format eq 'xml') {
        return Test::Lintian::Output::XML::to_universal($text);
    }

    die "Unknown format: $format\n";
}

sub usage {
    print <<"END";
Usage: $0 -f <format> <in-file> <out-file>

    --format, -f <format>  Format of Lintian output file <in-file>

    Extracts tag information from a variety of Lintian output formats. The
    output format is a simplified EWI format without letter code. Other
    notable differences are that the binary package type is always displayed.

    The tags are sorted in a reverse order, but with the package type pulled
    to the front. That way package types are grouped. Source packages are at
    the top.

    Prints to stdout when no <out-file> is given.
END
    return;
}

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
