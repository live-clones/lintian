#!/usr/bin/perl
#
# lintian-migrate-overrides-to-pointed-hints -- migrate lintian
# overrides to new "pointed hints" format.
#
# Copyright (C) 2022 Axel Beckert
#
# This program is free software.  It is distributed under the terms of
# the GNU General Public License as published by the Free Software
# Foundation; either version 2 of the License, or (at your option) any
# later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at https://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

use strict;
use warnings;
use 5.020; # Performance gain with $`, $&, and $'.

use Path::Tiny qw(path cwd);
use Getopt::Long ();

### Rules what needs to be changed how, i.e. kinda configuration
my %affected_tags = (
    # Syntax: 'tag' => [ qr/old/, 'new' ],
    'spelling-error-in-binary' => [ qr/(\S+) (\S*[ *]\S*)$/, '$2 [$1]' ],
    'package-contains-documentation-outside-usr-share-doc' =>
      [ qr/(.*)$/, '[$1]' ],
    'no-manual-page' => [ qr/(.*)/, '[$1]' ],
);

### Sanity check
die "Needs to be run from inside an unpacked Debian source package.\n"
  unless (-d 'debian' and -e 'debian/changelog' and -e 'debian/rules');

### Commandline option parsing
# Available commandline options
my @getoptions = qw(
  inline|i!
  compat|c|*
);

# Default values
my %command_line = (
    inline => -d '.git' ? 1 : 0,
    compat => 0,
);

# Configure commandline option parsing
Getopt::Long::Configure('default', 'bundling',
    'no_getopt_compat','no_auto_abbrev','permute');

# Parse the commandline and store the settings in %command_line
Getopt::Long::GetOptions(\%command_line, @getoptions)
  or die "error parsing options\n";

# Where to look for override files.
my @override_files
  = glob('debian/*lintian-overrides debian/source/lintian[-]overrides');

unless (@override_files) {
    warn "No override files found in the source package at ".cwd."/.\n";
    exit 0;
}

sub expand {
    local ($_) = @_;
    my $pattern = "$`$&$'";
    my @ms = @-;
    my @me = @+;
    s/\$(\d+)/substr($pattern, $ms[$1], $me[$1] - $ms[$1])/eg;
    s/[][]/*/g if $command_line{'compat'};
    $_;
}

foreach my $override_file (@override_files) {
    my $path = path($override_file);
    my @overrides = $path->lines_utf8();
    my @new_overrides = ();

    foreach my $override_line (@overrides) {
        # Don't migrate if it already looks like a pointed-hints override.
        unless (
            $override_line =~ m/\[.*\]$/
            or
            # Be a bit stricter when matching --compat generated overrides
            $override_line =~ m{\*\S*/\S*\*$}
        ) {

            foreach my $tag (keys %affected_tags) {
                my ($pattern, $transform) = @{$affected_tags{$tag}};
                $override_line
                  =~ s{^(?:[^#]*[^-]\b)?\Q$tag\E \K$pattern}{expand($transform)}e;
            }
        }

        if ($command_line{'inline'}) {
            push(@new_overrides, $override_line);
        } else {
            print $override_line;
        }
    }

    if (@new_overrides) {
        $path->spew_utf8(@new_overrides);
    }
}
