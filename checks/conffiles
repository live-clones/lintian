# conffiles -- lintian check script -*- perl -*-

# Copyright (C) 1998 Christian Schwarz
# Copyright (C) 2000 Sean 'Shaleh' Perry
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

package Lintian::conffiles;
use strict;
use warnings;

use Lintian::Tags qw(tag);
use Lintian::Util qw(fail);

sub run {

my (undef, undef, $info) = @_;

my $cf = $info->control('conffiles');


my %conffiles = ();

# Stop if conffiles is a link; no real package uses links in
# control.tar.gz.
return if -l $cf;

if (-f $cf) {

    open(IN, '<', $cf) or fail("cannot open $cf for reading: $!");
    while (<IN>) {
        chop;
        next if m/^\s*$/;

        unless (m,^/,) {
            tag 'relative-conffile', $_;
            $_ = '/' . $_;
        }
        my $file = $_;
        $file =~ s@^/++@@o;
        $conffiles{$file}++;

        if ($conffiles{$file} > 1) {
            tag 'duplicate-conffile', $file;
        }

        if (m,^/usr/,) {
            tag 'file-in-usr-marked-as-conffile', $file;
        } else {
            unless (m,^/etc/,) {
                tag 'non-etc-file-marked-as-conffile', $file;
            }
        }

    }
    close(IN);

}

# Read package contents...
foreach my $file ($info->sorted_index) {
    my $index_info = $info->index ($file);
    next unless $file =~ m,^etc, and $index_info->is_file;

    # If there is a /etc/foo, it must be a conffile (with a few exceptions).
    if (not exists($conffiles{$file})
        and $file !~ m,/README$,
        and $file ne 'etc/init.d/skeleton'
        and $file ne 'etc/init.d/rc'
        and $file ne 'etc/init.d/rcS') {
        tag 'file-in-etc-not-marked-as-conffile', $file;
    }
}

}

1;

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
