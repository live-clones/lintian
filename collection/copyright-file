#!/usr/bin/perl -w
# copyright-file -- lintian collector script

# Copyright (C) 1998 Richard Braakman
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

use strict;
use warnings;

use lib "$ENV{'LINTIAN_ROOT'}/lib";
use Lintian::Util qw(fail gunzip_file touch_file);

use File::Copy qw(copy);

($#ARGV == 2) or fail 'syntax: copyright-file <pkg> <type> <dir>';
my ($pkg, $type, $dir) = @ARGV;

if ( -e "$dir/copyright" ) {
    unlink "$dir/copyright" or fail "unlink copyright: $!";
}

my $file = "$dir/unpacked/usr/share/doc/$pkg/copyright";

# If copyright was a symlink, we need to make a copy of it.  Just hardlinking
# to the symlink may leave a relative symlink into a directory we can't
# unpack.  Be careful about what symlinks we allow, though.
if (-l $file) {
    my $link = readlink($file) or fail("cannot readlink $file: $!");
    if ($link =~ /\.\./ || ($link =~ m%/% && $link !~ m%^[^/]+(?:/+[^/]+)*\z%)) {
        touch_file "$dir/copyright";
    } else {
        copy ($file, "$dir/copyright") or fail "cannot copy $file: $!";
    }
} elsif (-f $file) {
    link ($file, "$dir/copyright")
        or fail("cannot link $file to copyright: $!");
} elsif (-f "$file.gz") {
    gunzip_file ("$file.gz", "$dir/copyright");
} else {
    # no copyright file found
    touch_file ("$dir/copyright");
}

exit 0;

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
