tag=sprintf("%s", ARG1)
# set terminal png truecolor small size 600,300
set terminal svg size 640,375 font "Sans,11"

set style fill solid 0.25

set style line 20 linetype 1 linecolor rgb "#1A77C9"
set style line 21 linetype 1 linecolor rgb "#83CDBE"
set style line 22 linetype 1 linecolor rgb "#BBBBBB"

set key outside

set xdata time
set timefmt "%s"
set format x "%b/%y"

set size 1.0,0.80
set origin 0.0,0.05

set nomxtics
set xtics nomirror scale 0.5,0 rotate by 55 offset -3.2,-2.7
set ytics nomirror scale 0.5,0
set offsets 0,0,1,0

load "common.gpi"

set xrange [ date_min : date_max ]
set yrange [ 0 : ]

set output sprintf("%s/tags/%s.svg", graph_dir, tag)
plot sprintf("%s/tags/%s.dat", history_dir, tag) \
    u 1:2 w filledcurve x1 ls 20 t 'Tags', \
 '' u 1:4 w filledcurve x1 ls 21 t 'Packages', \
 '' u 1:3 w filledcurve x1 ls 22 t 'Overridden'

